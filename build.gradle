plugins {
    id("java-library")
    id("maven-publish")
}

group = "cloud.grabsky"
version = "1.20.1-${System.getenv("GITHUB_RUN_NUMBER") ?: "DEV"}"

repositories {
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
}

dependencies {
    // JetBrains Annotations
    compileOnly("org.jetbrains:annotations:24.1.0")
    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.32")
    annotationProcessor("org.projectlombok:lombok:1.18.32")
    // Paper API
    compileOnly("io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT")
}

publishing {
    repositories {
        maven { name = "gpr"; url = "https://maven.pkg.github.com/Grabsky/commands"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
        maven { name = "vela"; url = "https://vela.grabsky.workers.dev/releases"
            credentials {
                username = System.getenv("VELA_USERNAME")
                password = System.getenv("VELA_PASSWORD")
            }
        }
    }
    publications {
        register("maven", MavenPublication.class) {
            from components.java
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

javadoc {
    (options as CoreJavadocOptions).addStringOption("Xdoclint:none", "-quiet")
}

tasks.register("release") {
    print("${version.toString().split('-')[0]} #${System.getenv("GITHUB_RUN_NUMBER")}")
}

tasks.register("tag") {
    print(version)
}
