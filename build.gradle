plugins {
    id 'java'
    id 'maven-publish'
    id 'com.palantir.git-version' version '0.15.0'
}

group = 'cloud.grabsky'
version = '0.1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

final isSnapshot = version.endsWith('-SNAPSHOT')

publishing {
    repositories {
        maven {
            name 'Cloud'
            url (isSnapshot == true ? 'https://repo.grabsky.cloud/snapshots' : 'https://repo.grabsky.cloud/releases')
            credentials {
                username System.getenv('REPO_USERNAME')
                password System.getenv('REPO_SECRET')
            }
            authentication { basic(BasicAuthentication) }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId rootProject.name
            version (isSnapshot == true ? versionDetails().gitHash : version)
            from components.java
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    (options as CoreJavadocOptions).addStringOption('Xdoclint:none', '-quiet')
}

repositories {
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    // lombok
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    // jetbrains annotations
    compileOnly 'org.jetbrains:annotations:24.0.0'
    // paper
    compileOnly 'io.papermc.paper:paper-api:1.19.3-R0.1-SNAPSHOT'
}
