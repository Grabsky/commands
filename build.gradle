plugins {
    id "java-library"
    id "maven-publish"
}

group = "cloud.grabsky"
version = "1.1-SNAPSHOT"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

final isSnapshot = version.endsWith("-SNAPSHOT")
final hash7 = "git rev-parse --verify --short HEAD".execute().text.trim()

publishing {
    repositories {
        maven {
            url = "https://maven.pkg.github.com/Grabsky/commands"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId = rootProject.name
            version = (isSnapshot == true ? hash7 : version)
            from components.java
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    (options as CoreJavadocOptions).addStringOption("Xdoclint:none", "-quiet")
}

repositories {
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
}

dependencies {
    // lombok
    compileOnly("org.projectlombok:lombok:1.18.24")
    annotationProcessor("org.projectlombok:lombok:1.18.24")
    // jetbrains annotations
    compileOnly("org.jetbrains:annotations:24.0.0")
    // paper
    compileOnly("io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT")
}

gradle.buildFinished {
    println()
    println("> " + (isSnapshot == true ? hash7 : project.version))
}
